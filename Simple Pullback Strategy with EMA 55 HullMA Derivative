//@version=5
strategy("Simple Pullback Strategy with EMA 55 HullMA Derivative", 
     overlay=true, 
     initial_capital=1000,
     default_qty_type=strategy.percent_of_equity, 
     default_qty_value=50, // 100% of balance invested on each trade
     commission_type=strategy.commission.cash_per_contract, 
     commission_value=0.01) // Interactive Brokers rate

// Get user input
i_ma1           = input.int(title="MA 1 Length", defval=200, step=10, group="Strategy Parameters", tooltip="Long-term MA")
i_ma2           = input.int(title="MA 2 Length", defval=10, step=10, group="Strategy Parameters", tooltip="Short-term MA")
i_emaLength     = input.int(title="EMA Length for Trend", defval=55, group="Strategy Parameters", tooltip="EMA Length to determine the trend")
i_stopPercent   = input.float(title="Stop Loss Percent", defval=0.01, step=0.01, group="Strategy Parameters", tooltip="Failsafe Stop Loss Percent Decline")
i_lowerClose    = input.bool(title="Exit On Lower Close", defval=false, group="Strategy Parameters", tooltip="Wait for a lower-close before exiting above MA2")
i_startTime     = input.time(title="Start Filter", defval=timestamp("01 Jan 1995 13:30 +0000"), group="Time Filter", tooltip="Start date & time to begin searching for setups")
i_endTime       = input.time(title="End Filter", defval=timestamp("1 Jan 2099 19:30 +0000"), group="Time Filter", tooltip="End date & time to stop searching for setups")


// Input for the Hull Moving Average period
hmaPeriod = input.int(14, title="HMA Period", minval=1)

// Calculate the Hull Moving Average
weightedClose = (high + low + 2 * close) / 4
sqrtPeriod = math.sqrt(hmaPeriod)
roundedSqrtPeriod = math.round(sqrtPeriod) // Round sqrtPeriod to nearest integer
hullMA = ta.wma(2 * ta.wma(weightedClose, hmaPeriod / 2) - ta.wma(weightedClose, hmaPeriod), roundedSqrtPeriod)

// Plot the HMA on the chart
plot(hullMA, color=color.blue, linewidth=2, title="Hull MA")


// Get indicator values
ma1 = ta.sma(close, i_ma1)
ma2 = ta.sma(close, i_ma2)
emaTrend = ta.ema(close, i_emaLength)

// CÃ¡lculo de la derivada aproximada de la SMA
derivadaHullMA = hullMA - hullMA[1]

// Determine the trend direction of EMA 55
trendUp = ta.ema(close, i_emaLength) > ta.ema(close[1], i_emaLength)

// Check filter(s)
f_dateFilter = time >= i_startTime and time <= i_endTime

// Check buy/sell conditions
var float buyPrice = 0
buyCondition    = close > ma1 and close < ma2 and strategy.position_size == 0 and f_dateFilter and trendUp and derivadaHullMA > -0.2
sellCondition   = close > ma2 and strategy.position_size > 0 and (not i_lowerClose or close < low[1])
stopDistance    = strategy.position_size > 0 ? ((buyPrice - close) / close) : na
stopPrice       = strategy.position_size > 0 ? buyPrice - (buyPrice * i_stopPercent) : na
stopCondition   = strategy.position_size > 0 and stopDistance > i_stopPercent

// Enter positions
if buyCondition
    strategy.entry(id="Long", comment="Long "  + str.tostring(derivadaHullMA), direction=strategy.long)

if buyCondition[1]
    buyPrice := open

// Exit positions
if sellCondition or stopCondition
    strategy.close(id="Long", comment="Exit "  + str.tostring(trendUp) + " " +  (stopCondition ? "SL=true" : ""))
    buyPrice := na

// Draw pretty colors
plot(buyPrice, color=color.lime, style=plot.style_linebr)
plot(stopPrice, color=color.red, style=plot.style_linebr, offset=-1)
plot(ma1, color=color.blue)
plot(ma2, color=color.orange)
plot(emaTrend, color=color.purple)

